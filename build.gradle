buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.5.5"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath "gradle.plugin.org.seasar.doma:codegen:1.4.1"
        classpath "gradle.plugin.org.seasar.doma:compile:1.1.0"
        classpath "mysql:mysql-connector-java:8.0.25"
        classpath "org.flywaydb:flyway-gradle-plugin:7.15.0"
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

subprojects {

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.flywaydb.flyway'

    idea.module.inheritOutputDirs = true
    group = 'jp.co.project.planets'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"
    [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:all,-options,-processing,-path"]

    repositories {
        mavenCentral()
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.5.5"
            mavenBom "org.junit:junit-bom:5.8.1"
        }
    }
    ext {
        datasourceHost = System.getenv("MYSQL_HOST") ?: "localhost"
        datasourcePort = System.getenv("MYSQL_PORT") ?: "3306"
        datasourceUsername = System.getenv("MYSQL_USER_NAME") ?: "planet"
        datasourcePassowrd = System.getenv("MYSQL_USER_PASSWORD") ?: "planet"
        datasourceSchema = System.getenv("MYSQL_SCHEMA") ?: "planet"
        datasourceUrl = "jdbc:mysql://$datasourceHost:$datasourcePort/$datasourceSchema?useSSL=false&allowPublicKeyRetrieval=true"
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        implementation 'org.seasar.doma:doma:2.29.0'

        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.apache.commons:commons-collections4:4.4'
        implementation 'com.google.guava:guava:31.0-jre'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'com.tngtech.archunit:archunit-junit5:0.21.0'
    }

    test {
        useJUnitPlatform()
    }
}